language Pyret {

  Lambda 2 {
    (* params body *)
    import 1;
    import 2;
    bind 1 in 2;
  }

  Apply 2 {
    import 1;
    import 2;
  }

  Arg 2 {
    import 1;
    import 2;
  }

  End 0 {}

  Param 2 {
    import 1;
    import 2;
    export 1;
    export 2;
  }

  Letrec 2 {
    (* binds, body *)
    import 1;
    import 2;
    export 1;
    export 2;
    bind 1 in 2;
  }

  LetrecBind 3 {
    (* name, value, binds *)
    import 1;
    import 2;
    import 3;
    export 3;
    bind 1 in 3;
    bind 3 in 1;
    bind 3 in 2;
  }

  LetBind 3 {
    (* name, value, binds *)
    import 1;
    import 2;
    import 3;
    export 3;
    bind 1 in 3;
  }

  LetExpr 2 {
    (* bindings, body *)
    import 1;
    import 2;
    bind 1 in 2;
  }


  (* For loops *)

  rule (For iter binds body)
    => (DesugarFor iter binds (End) (End) body)

  rule (DesugarFor iter (End) params args body)
     => (Apply iter (Arg (Lambda params body) args))

  rule (DesugarFor iter (ForBind param arg binds) params args body)
    => (DesugarFor iter binds (AppendParam param params) (AppendArg arg args) body)

  rule (AppendParam param (End))
    => (Param param (End))

  rule (AppendParam new_param (Param param params))
    => (Param param (AppendParam new_param params))

  rule (AppendArg arg (End))
    => (Arg arg (End))

  rule (AppendArg new_arg (Arg arg args))
    => (Arg arg (AppendArg new_arg args))


  (* Functions *)

  rule (Fun name params body rest_stmts)
    => (Letrec (LetrecBind name (Lambda params body) (End)) rest_stmts)


  (* Let statements *)

  rule (LetExpr binds (Let name value rest_stmts))
    => (LetExpr (AppendBind name value binds) rest_stmts)

  rule (Let name value rest_stmts)
    => (LetExpr (LetBind name value (End)) rest_stmts)

  rule (AppendBind name value (End))
    => (LetBind name value (End))

  rule (AppendBind _name _value (LetBind name value binds))
    => (LetBind name value (AppendBind _name _value binds))
}
