language Do {


  (* Functions *)
  
  (Lambda param body) {
    import param;
    import body;
    bind param in body;
  }
  (Param name params) {
    import name;
    import params;
    export name;
    export params;
  }
  (End) {}
  
  (Apply func args) {
    import func;
    import args;
  }
  (Arg arg args) {
    import arg;
    import args;
  }


  (* Multiple bodies *)

  (Void) {}
  (Begin expr rest) {
    import expr;
    import rest;
  }


  (* Set *)
  
  (Set var defn stmts) {
    import var;
    import defn;
    import stmts;
    export stmts;
  }


  (* Conditionals *)

  (If cond consq altern) {
    import cond;
    import consq;
    import altern;
  }


  (* TEMP *)

  (Let a b) {
    import a;
    import b;
    bind a in b;
  }

  (LetBind a b c) {
    import a;
    import b;
    import c;
    export a;
    export c;
  }

  (Letstar a b) {
    import a;
    import b;
    bind a in b;
  }

  (Letrec a b) {
    import a;
    import b;
    bind a in b;
  }

  (LetstarBind var init binds) {
    import var;
    import init;
    import binds;
    bind var in binds;
    export var;
    export binds;
  }

  (LetrecBind var init binds) {
    import var;
    import init;
    import binds;
    export var;
    export binds;
    bind var in binds;
    bind binds in var;
    bind var in init;
    bind binds in init;
  }


  sugar (Do binds test body)
  sugar (DoBindStep var init step binds)
  sugar (DoBind var init binds)
  sugar (DoTest test expr)


  rule (Do binds test commands)
    => (WrapDo (DsDo binds test commands))

  rule (DsDo (DoBind var init binds) test commands)
    => (DoBind var init (DsDo binds test commands))

  rule (DsDo (DoBindStep var init step binds) test commands)
    => (DoBindStep var init step (DsDo binds test commands))

  rule (DsDo (End) test commands)
    => (DsDoBinds test commands (End) (End) (End))

  rule (DoBind var init (DsDoBinds test commands inits vars steps))
    => (DsDoBinds test commands (Arg init inits) (Param var vars) (Arg as_refn$var steps))

  rule (DoBindStep var init step (DsDoBinds test commands inits vars steps))
    => (DsDoBinds test commands (Arg init inits) (Param var vars) (Arg step steps))

  rule (WrapDo (DsDoBinds (DoTest test exprs) commands inits vars steps))
    => (Letrec (LetrecBind @loop
         (Lambda vars
           (If test
             (Begin (Void) exprs)
             (Begin commands (Apply $loop steps)))) (End))
         (Apply $loop inits))

(*
(Do (DoBind @sum 0 (DoBindStep i 0 (+ i 1) (End)))
    ((= i 3) display)
    (set! sum (+ sum (read-line))))

(WrapDo
  (DsDo (DoBind @sum 0 (DoBindStep i 0 (+ i 1) (End)))
        ((= i 3) display)
        (set! sum (+ sum (read-line)))))

(DoBind @sum 0
  (DsDo (DoBindStep i 0 (+ i 1) (End))
        ((= i 3) display)
        (set! sum (+ sum (read-line)))))

(DoBind @sum 0
  (DoBindStep i 0 (+ i 1)
    (DsDo (End)
          ((= i 3) display)
          (set! sum (+ sum (read-line))))))

(DoBind @sum 0
  (DoBindStep i 0 (+ i 1)
    (DsDoBinds
      ((= i 3) display)
      (set! sum (+ sum (read-line)))
      (End) (End) (End))))

(DoBind @sum 0
  (DsDoBinds
    ((= i 3) display)
    (set! sum (+ sum (read-line)))
    (Arg 0 (End)) (Param i (End)) (Arg (+ i 1) (End)))

(DsDoBinds
  ((= i 3) display)
  (set! sum (+ sum (read-line)))
  (Arg 0 (Arg 0 (End)))
  (Param @sum (Param @i (End)))
  (Arg $sum (Arg (+ i 1) (End))))

(Letrec (LetrecBind @loop
  (Lambda @sum @i
    (If (= i 3)
      display
      (Begin (set! sum (+ sum (read-line))) (Apply $loop $sum (+ i 1))))))
  (Apply $loop 0 0))
*)

}