language NamedLet {



  (* Functions *)
  
  (Lambda param body) {
    import 1;
    import 2;
    bind 1 in 2;
  }
  (Param name params) {
    import 1;
    import 2;
    export 1;
    export 2;
  }
  (End) {}
  
  (Apply func args) {
    import 1;
    import 2;
  }
  (Arg arg args) {
    import 1;
    import 2;
  }



  (* Letrecs *)

  (Letrec binds body) {
    import 1;
    import 2;
    bind 1 in 2;
  }
  (Bindrec name defn binds) {
    import 1;
    import 2;
    import 3;
    bind 1 in 3;
    bind 3 in 2;
    export 3;
  }


  (* Regular (parallel) Let *)
  
  (Let binds body) {
    import 1;
    import 2;
    bind 1 in 2;
  }
  (Bind name defn binds) {
    import 1;
    import 2;
    import 3;
    export 1;
    export 3;
  }


  (* Named-Let sugar *)

  sugar (NamedLet proc_id binds body)
  sugar (NamedLetBind arg_id init_expr binds)

  rule (NamedLet proc_id binds body)
    => (DesugarNamedLet proc_id binds body (End) (End))

  rule (DesugarNamedLet proc_id (End) body params args)
    => (Letrec
         (Bindrec @f (Lambda params (Let (Bind proc_id $f (End)) body))
           (End))
         (Apply $f args))

  rule (DesugarNamedLet proc_id (NamedLetBind arg_id init_expr binds) body params args)
    => (DesugarNamedLet proc_id binds body (AppendParam arg_id params) (AppendArg init_expr args))

  rule (AppendParam param (End))
    => (Param param (End))

  rule (AppendParam new_param (Param param params))
    => (Param param (AppendParam new_param params))

  rule (AppendArg arg (End))
    => (Arg arg (End))

  rule (AppendArg new_arg (Arg arg args))
    => (Arg arg (AppendArg new_arg args))

}