language ListComprehension {


  (* Haskell Lets are letrecs *)

  (Let binds body) {
    import binds;
    import body;
    bind binds in body;
  }
  (Bind name defn binds) {
    import name;
    import defn;
    import binds;
    bind name in binds;
    bind binds in defn;
    export binds;
  }
  (End) {}


  (* Case Expressions: `case expr of pattern -> template | ...` *)

  (Case expr cases) {
    import expr;
    import cases;
  }
  (ACase pattern template rest) {
    import pattern;
    import template;
    import rest;
    bind pattern in template;
  }
  (Underscore) {}


  (* If-then-else *)

  (If condition then else) {
    import condition;
    import then;
    import else;
  }


  (* Functions *)

  (Lambda pattern expr) {
    import pattern;
    import expr;
    bind pattern in expr;
  }
  (Apply func arg) {
    import func;
    import arg;
  }


  (* Lists *)

  (Cons head tail) {
    import head;
    import tail;
  }
  (Empty) {}
  


  (* List Comprehension Sugar *)
  (* See Haskell Standard, section 3.11: List Comprehensions *)
  (* https://www.haskell.org/onlinereport/exps.html#sect3.11 *)

  sugar (ListCompr expr qualifiers)
  sugar (BooleanGuard expr qualifiers)
  sugar (Generator pattern expr qualifiers)
  sugar (LocalBind decls qualifiers)

  (* [ e | True ] = [ e ]           *)
  (* [ e | q ]    = [ e | q, True ] *)
  rule (ListCompr e (End))
    => (Cons e (Empty))

  (* [ e | b, Q ]  = if b then [ e | Q ] else [] *)
  rule (ListCompr e (BooleanGuard b q))
    => (If b (ListCompr e q) (Empty))

  (* [ e | p <- l, Q ]    = let ok p = [ e | Q ]  *)
  (*                            ok _ = []          *)
  (*                        in concatMap ok l      *)
  rule (ListCompr e (Generator p l q))
    => (Let (Bind @ok (Lambda @x (Case $x
                                   (ACase p (ListCompr e q)
                                   (ACase (Underscore) (Empty)
                                   (End)))))
            (End))
         (Apply (Apply global$concatMap $ok) l))

  (* [ e | let decls, Q ]  =  let decls in [ e | Q ] *)
  rule (ListCompr e (LocalBind decls q))
    => (Let decls (ListCompr e q))

}